# chore flags
default swept_foyer = False
default dusted_books = False
default collected_dishes = False
default chores_left = 3
default TOTAL_CHORES = 3

label cafe_intro:
    scene cafe with fade
    show emi with dissolve
    emi "Thank you for coming!"
    emi "(Phew, that's the last customer! I can finally catch my breath. . .)"
    narrator "A bell chimes at the front of the store, signaling the end of the chaotic lunch hour."
    narrator "Today had been unusually hectic at the cafe. The morning hours were peaceful, with only a handful of regulars popping in for their usual coffee or bagel."
    narrator "As the sun climbed higher in the sky, more and more customers trickled in, looking for a refreshment to enjoy."
    narrator "Before I knew it, the store had filled up in the blink of an eye. I was scrambling in every direction, jotting down orders and relaying them to Ingram. Somehow, I made it through. Just barely."
    narrator "I collapse into a chair at an empty table, stretching my arms to relieve the tension in my muscles." 
    narrator "Out of nowhere, a light poke taps at my shoulder. I turn to see Ingram standing there, with two icy drinks in hand."
    show emi at right with move
    show ingram at left with dissolve
    ingram "Good job, you managed pretty well out there."
    ingram "Here,{w=0.1} I made some iced coffee for us. I figured you might need a little treat to cool off and recharge. Go on, take a sip."
    narrator "Ingram takes the seat in front of me, passing one of the drinks my way. I take the glass, divulging in the drink with modest sips to avoid spilling coffee on myself."
    emi "(Oh? This coffee. . .)"
    narrator "My tastebuds are greeted by a delightful sweetness, a subtle hint of vanilla hidden within. In an instant, my sweet tooth kicks in, tempting me to devour the whole thing."
    narrator "I indulge in the icy goodness and small sips quickly turn into bigger gulps. Next thing I know, there are only a few ice cubes remaining at the bottom of my glass."
    emi "Thanks for the drink, you're a lifesaver!"
    narrator "I look at Ingram, who is still finishing his drink. The ice cubes clink softly as he downs the rest of it."
    narrator "Despite the chaos we just went through, he seems completely composed, almost as if he's unfazed by the rush. If I didn't know any better, I'd find it hard to believe there was even a crowd."
    emi "You know Ingram, you're really impressive."
    emi "You handled yourself so well behind the counter. I never even knew a cafe could get that busy!"
    narrator "Ingram nods in acknowledgment, setting his glass down with a solid thud."
    ingram "It's all about finding the right rhythm and keeping things organized."
    ingram "It's natural to feel overwhelmed at first. Eventually, you'll be able to breeze right through the rush hours as well."
    narrator "I gulp nervously at Ingram's words. Maybe he just means well, but the pressure feels kind of. . . intimidating."
    narrator "The idea of being able to deal with so many orders at once still seems like a faraway dream for me. I can't imagine ever being on the same level as him."
    emi "(Maybe in a million years, I'll get there. . .surely. . .)"
    narrator "Ingram gets up from the table, collecting the two glasses as he heads back to the counter. I quickly follow suit, feeling somewhat rejuvenated from the short coffee break."
    ingram "Well, now that we've caught our breath, it's time to tackle the next tasks on our list."
    narrator "Ingram's words catch me off guard, and I nearly choke."
    emi "Wait, what? There's more?"
    ingram "Hmm? Of course, we're only halfway through the day. There's still lots to do."
    narrator "I feel my heart sink. The happy, nonchalant mood from earlier disappears, replaced by a looming sense of dread as I'm reminded that the day isn't over yet."
    narrator "I glance over at Ingram, who's already starting to wipe down the countertops and fill the coffee grinders."
    emi ". . .*sigh*"
    emi "Alright then. What do we have to do?"
    narrator "Ingram glances over at me, his smug smile unwavering. He seems almost gleeful, as if he's looking forward to putting me to work."
    ingram "Well for starters, there's a little housekeeping that needs to be done." 
    ingram "Let's see. . ."
    ingram "Well, the foyer needs some sweeping. . .{w=0.1}and there's empty dishes that need to be collected from the tables. {w=0.1}Oh, and the library corner could use some dusting."
    emi ". . ."
    emi "We still have to do all that. . .?"
    ingram "Mmhm. There's always something to do, but don't feel too pressured."
    ingram "You have all day to get it done. Just take it one task at a time, and it'll be over before you know it."
    emi "Urgh. . . {w=0.5}well, I guess it has to be done one way or another."
    emi "What about you then? {w=0.5}What are you gonna be doing?"
    ingram "Well, someone has to be there to handle the customers right? {w=0.3}I'll be doing my barista duties and manning the cash register."
    ingram "You're still learning the ropes, so I think it's best if I handle those tasks for now."
    ingram "At least, until you're more comfortable with everything."
    emi "Oh. . .I guess that makes sense."
    ingram "Alright, let's get down to business then. {w=0.5} I'm going to go refill some of the pastries in the display case. You can find anything you need in the back."
    emi "Ah, yes boss!"
    hide ingram with dissolve
    show emi at center with move
    narrator "As Ingram heads over to the other side of the counter, I turn around to look for supplies in the back."
    emi "(Okay, let's do this!)"
    emi "(I should follow Ingram's advice and just focus on one thing at a time. This will all be over in the blink of an eye!)"
    narrator "I smack my face twice in an attempt to pump myself up."
    ingram ". . .pfft."
    show ingram at left with dissolve 
    show emi at right with move
    narrator "A snicker creeps up from behind. I spin around, finding myself face to face with Ingram once again."
    narrator "He covers his mouth in a futile attempt to stifle his laugh, his entire body shaking as he struggles to contain it."
    narrator "I could feel my cheeks burning with embarrassment."
    ingram "Sorry, I didn't mean to interrupt your. . ."
    emi "I thought you went to go fill up the display case!"
    ingram "I was going to, but I forgot to tell you something."
    ingram "Though I didn't expect you to slap yourself as soon as I turned my back. . ."
    narrator "Ingram nearly slips at the end of his sentence, but manages to keep himself together."
    emi "(. . .isn't he enjoying this a little too much?)"
    narrator "I feel the burning sensation from my cheeks spread up to my ears."
    emi "(I wish I can just jump in a hole and bury myself six feet under. I'm gonna die of embarrassment!)"
    emi "(I better change the subject before he says anything else. . .)"
    emi "A-anyway! What was it that you wanted to tell me?"
    ingram "Right, I wanted to let you know that you might meet some of the others who also live at Yuugen House."
    ingram "They often lounge around the cafe at this time."
    ingram "It might be a good chance for you to introduce yourself and get to know them a bit if you'd like."
    emi "That's fair. What are their names?"
    ingram "There's [kai_name!t], [annelise_name!t], and [akira_name!t]. {w=0.3}I've mentioned you before, so they should be aware of who you are."
    ingram "If you haven't met any of them already, today might be your chance. I'm sure they'll be happy to meet you."
    emi "[kai_name!t], [annelise_name!t] and [akira_name!t]...{w=0.5} alright, {w=0.1}I'll remember that."
    emi "Well then, {w=0.1}I'll be back soon!"
    ingram "Sounds good. {w=0.3}I'll be right here if you need anything."
    ingram "Just don't go beating yourself up unprompted again."
    emi "Ahhh!! Stop bringing it up!"
    narrator "Ingram smiles as I try and shoo him away."
    hide ingram with dissolve
    show emi at center with move
    narrator "Once he's out of sight, I start making my way over to the back storage room."
    
    scene storage_room with fade
    show emi with dissolve
    emi "Whoa! This place is surprisingly clean."
    narrator "I'm pleasantly surprised by the level of organization."
    narrator "Rows of shelves line the walls from floor to ceiling, holding various cleaning supplies such as disinfectant spray bottles, sponges and cleaning cloths. They are all meticulously arranged by type, making them easy to find and access."
    narrator "On the opposite end of the room, a large cooler stands. It seems to be well-maintained, containing unopened cartons of milk, cream and other baking ingredients."
    narrator "Running a finger along one of the shelves, I find that there's not a speck of dust to be found. The place was absolutely spotless."
    emi "(Did Ingram do all of this too? He really is on a different level!)"
    narrator "I look around for the tools I need to finish my chores, spotting a broom in the corner, feather duster hanging on the wall, and some neatly folded rags on a shelf."
    emi "(Hmm. . . what to do first?)"
    call chores from _call_chores
    return

# prompts for chores, will only return when all done
label chores:
    while not (swept_foyer and dusted_books and collected_dishes):
        if chores_left == 1:
            $ text = "I'm making good progress! Now I just need to. . ."
        elif chores_left == TOTAL_CHORES:
            $ text = "(Hmm. . .what to do first?)"
        else:
            $ text = "Now I should. . ."
        
        menu:
            emi "[text]"
            "Sweep the foyer" if not swept_foyer:
                call meeting_kai from _call_meeting_kai

            "Dust the library books" if not dusted_books:
                call meeting_annelise from _call_meeting_annelise

            "Collect empty dishes" if not collected_dishes:
                call meeting_akira from _call_meeting_akira
    return
